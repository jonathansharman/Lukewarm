import Graphics2D;
import Math;

const BULLET_RADIUS = 3.0;
const BULLET_DIAMETER = 2 * BULLET_RADIUS;
const BULLET_SPEED = 15.0;
const BULLET_TRAIL_FACTOR = 8.0;
const BULLET_SPAWN_DISTANCE = CHARACTER_RADIUS + BULLET_RADIUS;

// A deadly projectile.
class Bullet : Body {
	field initialPos;

	constructor(pos, vel) : base(pos, vel, BULLET_RADIUS) {
		this.initialPos = pos.clone();
	}

	function draw() {
		trail = this.pos.minus(this.vel.times(BULLET_TRAIL_FACTOR));
		if (this.pos.minus(this.initialPos).lengthSquared() < this.pos.minus(trail).lengthSquared()) {
			trail = this.initialPos;
		}
		Graphics2D.Draw.line
			( Math.floor(this.pos.x + 0.5)
			, Math.floor(this.pos.y + 0.5)
			, Math.floor(trail.x + 0.5)
			, Math.floor(trail.y + 0.5)
			, BULLET_DIAMETER
			, 255, 0, 0
			);
		Graphics2D.Draw.ellipse
			( Math.floor(this.pos.x - this.radius + 0.5)
			, Math.floor(this.pos.y - this.radius + 0.5)
			, 2.0 * this.radius
			, 2.0 * this.radius
			, 0, 0, 0
			);
	}
}
